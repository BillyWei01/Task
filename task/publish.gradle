apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

def GROUP = 'com.horizon.task'
def ARTIFACT = 'task'
def VERSION = '1.0.6'

group = GROUP
version = VERSION

dokka{
    outputFormat = 'javadoc'
    moduleName = project.name
    outputDirectory = "$buildDir/docs"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    //classifier = 'javadoc'
    getArchiveClassifier().set('javadoc')
    from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
    //classifier = 'sources'
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

def srcClassDir = [project.buildDir.absolutePath + "/intermediates/classes/release",
                   project.buildDir.absolutePath + "/tmp/kotlin-classes/release"]

task makeJar(dependsOn: ['compileReleaseSources'], type: Jar) {
    delete 'build/libs/' +ARTIFACT + '.jar'
    baseName  ARTIFACT
    extension = "jar"
    from srcClassDir
    exclude('**/R.class')
    exclude('**/BuildConfig.class')
    exclude { it.name.startsWith('R$') }
}

artifacts {
    archives javadocJar
    archives sourcesJar
    archives makeJar
}

def siteUrl = 'https://github.com/No89757/Task'
def gitUrl = 'https://github.com/No89757/Task.git'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Task is a thread scheduling framework.'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'no89757'
                        name 'John Smith'
                        email 'across_horizon@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        userOrg = "horizon757"
        repo = "maven"
        name = "Task"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
